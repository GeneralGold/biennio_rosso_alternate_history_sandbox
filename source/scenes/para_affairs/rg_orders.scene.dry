title: Red Guard Orders
new-page: true
is-card: true
tags: para_affairs
view-if: rg_orders_timer <= 0 and participating = 0 and rg_banned = 0
card-image: img/RedGuards.jpg
on-arrival: week_actions += 1; rg_orders_timer = 3;

= Red Guards

The *Guardie Rosse*, following our continued goal of violent revolution, will be pivotal to our cause and as such, we can command the organisation to aid our cause.

- @fighting: Utilise the Red Guards and test our forces against the fascists.
- @occupations: Utilise the Red Guards to occupy factories.
- @protests: Utilise the Red Guards to strike and demand better conditions.
- @return: Keep the Red Guards as it is.
- @easy_discard
- @cancel_advisor_action

@fighting
subtitle: -3 Paramilitary Resources - Test the Red Guards against the Arditsmo.
choose-if: para_resources >= 3
unavailable-subtitle: Not enough paramilitary resources. (Need 3)
on-arrival: para_resources -= 3; radicalisation += 0.1; revolutionary_fervour += 5; rg_strength -= 30*revolutionary_exhaustion; rg_militancy += 0.03*revolutionary_exhaustion; ard_strength -= 30*rg_militancy; revolutionary_exhaustion += 0.03 if not_participating = 1; middle_class_psi -= 6*(1-dissent); 

Our forces begin intercepting and picking fights with the Arditismo, allowing them to gain combat experience and reinvigorating the party.

@occupations
subtitle: -1 Paramilitary Resources - Aid factory workers to seize control of their workplaces.
choose-if: para_resources >= 1
unavailable-subtitle: Not enough paramilitary resources. (Need 1)
on-arrival: para_resources -= 1; radicalisation += 0.05; revolutionary_fervour += 3; rg_strength -= 10*revolutionary_exhaustion; workers_psi += 4; revolutionary_exhaustion += 0.02 if not_participating = 1; industrialists_psi -= 4; factory_takeovers += 1;  middle_class_psi -= 4*(1-dissent); 

We have seized control of more factories and kicked the bourgeois industrialists out! This has reinvigorated the party, and convinced more workers to our side.

@protests
subtitle: -1 Paramilitary Resources - Protest and strike against the current working conditions.
choose-if: para_resources >= 1
unavailable-subtitle: Not enough paramilitary resources. (Need 1)
on-arrival: para_resources -= 1; radicalisation += 0.02; revolutionary_fervour += 1; rg_strength -= 5*revolutionary_exhaustion; workers_psi += 2; revolutionary_exhaustion += 0.01 if not_participating = 1; industrialists_psi -= 2;  middle_class_psi -= 2*(1-dissent); 

We utilise the Red Guards to aid the workers, forcing the hands of the industrialists to grant better conditions for the workers such as higher wages, lower hours and better benefits.
